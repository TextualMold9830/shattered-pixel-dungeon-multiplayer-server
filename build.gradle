buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.2'
    }
}

allprojects {
    // load version from file
    def versionFile = new File(rootProject.rootDir, "version")
    def versionName = "Local-build"

    if (versionFile.exists()) {
        try {
            versionName = versionFile.text.trim()
        } catch (Exception e) {
            println("Error reading version file: ${e.message}")
        }
    } else {
        println("Version file not found, using default: 'Local-build'")
    }
    def baseVersionName = "3.3.5"
    ext {
        appName = 'Shattered Pixel Dungeon Server'
        appPackageName = 'com.shatteredpixel.shatteredpixeldungeon.multiplayer.server'

        appVersionCode = 890
        appVersionName = "Server_" +versionName + "_on_v"+baseVersionName
        appShortVersionName = ((appVersionName) =~ /\d+\.\d+\.\d+/) ? ((appVersionName) =~ /\d+\.\d+\.\d+/)[0] : "0.0.0"

        appJavaCompatibility = JavaVersion.VERSION_11

        appAndroidCompileSDK = 35   //Android 15
        appAndroidMinSDK = 21       //Android 5.0
        appAndroidTargetSDK = 35    //Android 15

        gdxVersion = '1.14.0'
        gdxControllersVersion = '2.2.4'
        robovmVersion = '2.3.24'
    }
    version = appVersionName

    repositories {
        google()
        mavenCentral()
        maven { url 'https://central.sonatype.com/repository/maven-snapshots/' }
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xmaxerrs" << "40000" << "-Xlint" //<< " -Xmaxwarns" << "1000"
        }
    }
}