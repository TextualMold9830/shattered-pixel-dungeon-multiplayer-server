buildscript {
    repositories {
        google()
        mavenCentral()
        maven {
            url = uri("https://storage.googleapis.com/r8-releases/raw")
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.1'
    }
}

allprojects {
    // load version from file
    def versionFile = new File(rootProject.rootDir, "version")
    def versionName = "Local-build"

    if (versionFile.exists()) {
        try {
            versionName = versionFile.text.trim()
        } catch (Exception e) {
            println("Error reading version file: ${e.message}")
        }
    } else {
        println("Version file not found, using default: 'Local-build'")
    }
    def baseVersionName = "3.0.2"
    ext {
        appName = 'Shattered Pixel Dungeon Server'
        appPackageName = 'com.shatteredpixel.shatteredpixeldungeon.multiplayer.server'

        appVersionCode = 833
        appVersionName = "Server_" +versionName + "_on_v"+baseVersionName
        appShortVersionName = ((appVersionName) =~ /\d+\.\d+\.\d+/) ? ((appVersionName) =~ /\d+\.\d+\.\d+/)[0] : "0.0.0"

        appJavaCompatibility = JavaVersion.VERSION_1_8

        appAndroidCompileSDK = 34
        appAndroidMinSDK = 14
        appAndroidTargetSDK = 34

        gdxVersion = '1.12.1'
        gdxControllersVersion = '2.2.4-SNAPSHOT'
        robovmVersion = '2.3.21'
    }
    version = appVersionName

    repositories {
        google()
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xmaxerrs" << "40000" << "-Xlint" //<< " -Xmaxwarns" << "1000"
        }
    }
}